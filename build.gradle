plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.1'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.hidetake.swagger.generator' version '2.19.2'
}

group = 'com.goalmate'
version = '0.0.1'

java {
    sourceCompatibility = 17
    targetCompatibility = 17
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/milestone' }
}

dependencies {

    // Spring Boot & Web
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-hateoas'
//    implementation 'org.springframework.boot:spring-boot-starter-security'
    runtimeOnly 'com.mysql:mysql-connector-j'

    // Jwt
    implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
    implementation 'io.jsonwebtoken:jjwt-impl:0.12.6'
    implementation 'io.jsonwebtoken:jjwt-jackson:0.12.6'

    // openFeign
    implementation platform("org.springframework.cloud:spring-cloud-dependencies:2024.0.0")
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign:4.0.3'
    implementation 'org.springframework.cloud:spring-cloud-starter-loadbalancer'

    // Jackson & XML
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml'

    // Springfox/Swagger
    implementation 'io.springfox:springfox-oas:3.0.0'

    // compileOnly
    annotationProcessor 'org.projectlombok:lombok'
    compileOnly 'io.swagger:swagger-annotations:1.6.4'
    compileOnly 'org.springframework.boot:spring-boot-starter-validation'
    compileOnly 'org.openapitools:jackson-databind-nullable:0.2.3'

    // Swagger Code Generation Plugins/Tools
    swaggerCodegen 'org.openapitools:openapi-generator-cli:6.2.1'
    swaggerUI 'org.webjars:swagger-ui:3.52.5'

    // Test Scope
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

swaggerSources {
    def typeMappings = 'URI=URI'
    def importMappings = 'URI=java.net.URI'
    goalmate {
        def apiYaml = "${rootDir}/src/main/resources/api/goalmate-spec.yaml"
        def configJson = "${rootDir}/src/main/resources/api/config.json"
        inputFile = file(apiYaml)
        def ignoreFile = file("${rootDir}/src/main/resources/api/.openapi-generator-ignore")
        code {
            language = 'spring'
            configFile = file(configJson)
            rawOptions = ['--ignore-file-override', ignoreFile, '--type-mappings',
                          typeMappings, '--import-mappings', importMappings] as List<String>
            components = [models: true, apis: true, supportingFiles: 'ApiUtil.java']
            dependsOn validation
        }
    }
}

compileJava.dependsOn swaggerSources.goalmate.code
sourceSets.main.java.srcDir "${swaggerSources.goalmate.code.outputDir}/src/main/java"
sourceSets.main.resources.srcDir "${swaggerSources.goalmate.code.outputDir}/src/main/resources"

tasks.named('test') {
    useJUnitPlatform()
}

build {
    dependsOn generateSwaggerUI
}

processResources {
    dependsOn generateSwaggerCode
    dependsOn generateSwaggerUI
    from("${swaggerSources.goalmate.ui.outputDir}") {
        into 'static/swagger-ui'
    }
}
